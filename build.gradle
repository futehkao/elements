ext.elementsVersion = '2.6.24'
ext.junitPlatformLauncherVersion = '1.6.2'
ext.junitJupiterVersion = '5.6.2'
ext.snakeyamlVersion = '1.27'
ext.simplejndiVersion = '0.11.4.1'
ext.findbugsVersion = '3.0.2'
ext.lmaxDisruptorVerions = '3.4.2'
ext.slf4jVersion = '1.7.30'
ext.groovyVersion = '3.0.7'
ext.guavaVerions = '30.1-jre'
ext.akkaVersion = '2.6.9'
ext.akkaScalaVersion = '2.13'
ext.aeronVersion = '1.30.0'
ext.kryoVersion = '5.0.0-RC9'
ext.bytebuddyVersion = '1.10.16'
ext.ehcacheVersion = '3.9.0'
ext.javaCacheApiVerison = '1.1.1'
ext.javamailVersion = '1.6.2'
ext.javaxWsRsApiVersion = '2.1.6'
ext.javaeeApiVersion = '8.0.1'
ext.jacksonVersion = '2.11.2'
ext.jettyVersion =  '9.4.40.v20210413'
ext.tomcatVersion = '9.0.38'
ext.tomcatLog4jVersion = '9.0.0.M6'
ext.log4jVersion = '2.13.3'
ext.cxfVersion = '3.4.3'
ext.httpclientVersion = '4.5.13'
ext.asmVersion = '9.0'  // network
ext.cassandraDriver4Version = '4.9.0'
ext.reactiveStreamsVersion = '1.0.3'
ext.opendmkVersion = '1.0-b02'
ext.simplejmxVersion = '1.17'
ext.quartzVersion = '2.3.2'
ext.hikariVersion = '3.4.5'
ext.mariadbClientVersion = '2.6.2'
ext.hibernateVersion = '5.4.27.Final'  // note 5.4.6 - 5.4.8 have major stack overflow issues
ext.bouncycastleVersion = '1.68'
ext.jaxbVersion = '2.3.1'      // security
ext.jaxbRuntimeVersion = '2.3.3'
ext.jaxbOldVersion = '2.3.0'
ext.jnrffiVersion = '2.1.16'  // cassandra and akka cluster typed

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:6.0.2'
    }
}

allprojects {
    group 'net.e6tech'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'

    sourceCompatibility = 1.8

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:syntax', '-quiet')
    }

    repositories {
        mavenCentral()
    }

    configurations {
        compile.exclude(group: 'jakarta.annotation') // replaced with javaee-api, which used by hibernate
        compile.exclude(group: 'jakarta.activation') // in javaee
        compile.exclude group: 'javax.activation' // in javaee, but we need an implementation, which is in jaxb-runtime
        compile.exclude(group: 'jakarta.ws.rs') // replaced with javaee-api from network
        compile.exclude(group: 'javax.xml.bind')
        // replaced with jakarta.xml.bind which comes from org.glassfish.jaxb:jaxb-runtime
        compile.exclude(group: 'javax.persistence')    // in javaee
        // compile.exclude(group: 'javax.servlet') // This is needed because LocalStrings resource bundle is missing from javaee
    }
}

dependencies {
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompile("org.junit.platform:junit-platform-launcher:${junitPlatformLauncherVersion}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
}

subprojects {

    group 'net.e6tech'
    version "${elementsVersion}"

    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'
    apply plugin: 'signing'

    archivesBaseName = "elements-${project.name}"

    signing {
        sign configurations.archives
    }

    javadoc {
        source = sourceSets.main.allJava
        classpath = configurations.compile
    }

    apply plugin: 'org.owasp.dependencycheck'
    dependencyCheck {
        suppressionFile = 'owasp.xml'
    }

    task sourcesJar(type: Jar) {
        archiveBaseName.set "elements-${project.name}"
        archiveClassifier.set 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            common(MavenPublication) {
                from components.java
                artifactId "elements-${project.name}"

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    description "elements-${project.name}"
                    packaging 'jar'
                    // optionally artifactId can be defined here. description 'A application used as an example on how to set up pushing its components to the Central Repository.'
                    url 'http://github.com/futehkao/elements.git'

                    scm {
                        connection 'scm:git:https://github.com/futehkao/elements.git'
                        developerConnection 'scm:git:https://github.com/futehkao/elements.git'
                        url 'https://github.com/futehkao/elements.git'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'futeh'
                            name 'Futeh Kao'
                            email 'futeh@episodesix.com'
                        }
                    }
                }
            }
        }
    }
}
